project(qt-videoplayer)
cmake_minimum_required(VERSION 2.8.9)
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../cmake/modules)
set(CMAKE_AUTOMOC ON)
set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(QTGSTREAMER_VERSION 1.2.0)
set(Qt4_MIN_VERSION 4.7)
set(Qt5_MIN_VERSION 5.0.0)

find_package(Qt4or5 COMPONENTS Core Gui Widgets Quick1 REQUIRED)
if (${QT_VERSION} STREQUAL "5")
    set(USE_QT5 TRUE)
    set(QTGLIB_LIBRARY Qt5GLib)
    set(QTGSTREAMER_LIBRARY Qt5GStreamer)
    set(QTGSTREAMER_QUICK_LIBRARY Qt5GStreamerQuick)
    set(QTGSTREAMER_UI_LIBRARY Qt5GStreamerUi)
    set(QTGSTREAMER_UTILS_LIBRARY Qt5GStreamerUtils)
    set(QTGSTREAMER_PACKAGE_NAME Qt5GStreamer)
    set(QTVIDEOSINK_NAME qt5videosink)
    set(QTGLVIDEOSINK_NAME qt5glvideosink)
    set(QWIDGETVIDEOSINK_NAME qwidget5videosink)
elseif (${QT_VERSION} STREQUAL "4")
    set(USE_QT4 TRUE)
    set(QTGLIB_LIBRARY QtGLib)
    set(QTGSTREAMER_LIBRARY QtGStreamer)
    set(QTGSTREAMER_QUICK_LIBRARY "")
    set(QTGSTREAMER_UI_LIBRARY QtGStreamerUi)
    set(QTGSTREAMER_UTILS_LIBRARY QtGStreamerUtils)
    set(QTGSTREAMER_PACKAGE_NAME QtGStreamer)
    set(QTVIDEOSINK_NAME qtvideosink)
    set(QTGLVIDEOSINK_NAME qtglvideosink)
    set(QWIDGETVIDEOSINK_NAME qwidgetvideosink)
endif()

if (${QT_VERSION} STREQUAL "5")
    find_package(Qt5GStreamer REQUIRED)
else()
    find_package(QtGStreamer REQUIRED)
endif()
find_package(OpenGL)
find_package(OpenGLES2)

include_directories(${QTGSTREAMER_INCLUDES})
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${QTGSTREAMER_FLAGS}")
add_definitions(${QTGSTREAMER_DEFINITIONS})

if (Qt4or5_OpenGL_FOUND AND (OPENGL_FOUND OR OPENGLES2_FOUND))
    if (OPENGLES2_FOUND)
        include_directories(${OPENGLES2_INCLUDE_DIR})
    else()
        include_directories(${OPENGL_INCLUDE_DIR})
    endif()
else()
    add_definitions(-DQMLPLAYER_NO_OPENGL)
endif()

set(videoplayer_SOURCES main.cpp player.cpp)
qt4or5_add_resources(videoplayer_rcc_SOURCES qmlplayer.qrc)

add_executable(videoplayer
    ${videoplayer_SOURCES}
    ${videoplayer_rcc_SOURCES}
)
target_link_libraries(videoplayer ${QTGSTREAMER_UI_LIBRARIES})
qt4or5_use_modules(videoplayer Core Gui Widgets Quick1)
if (Qt4or5_OpenGL_FOUND AND (OPENGL_FOUND OR OPENGLES2_FOUND))
    qt4or5_use_modules(videoplayer OpenGL)
endif()
